coding UTF-8;

# PRAGMA COMPILE    REFERRAL
# PRAGMA RESERVE    ALL
# PRAGMA END

import GUI;
import gui.ButtonPanel;

/**
 * <p>
 * このライブラリ gui.ButtonWindow は、
 * GUI部品として、ボタンが並ぶウィンドウ ―― ボタンウィンドウ ―― を提供します。	
 * </p>
 *
 * <p>
 * VCSSLの標準ライブラリ「 GUI 」では、GUI部品をint型のIDで扱いますが、
 * このモジュールで提供されるGUI部品は、
 * <a href="#ButtonWindow">ButtonWindow</a> 構造体として扱います。
 * </p>
 *
 * <p>
 * ButtonWindow 構造体は、内部にリソースのID等を保持していますが、
 * <br />
 * 生成・破棄のための関数が提供されるため、その中身を特に意識する必要はありません（ むしろメンバへの直接的なアクセスは推奨されません ）。
 * <br />
 * 具体的には、 <a href="#newButtonWindow">newButtonWindow</a> 関数で生成し、 
 * <a href="#deleteComponent">deleteComponent</a> 関数で破棄します。
 * </p>
 *
 * <p>
 * またこのモジュールでは、
 * ButtonWindow 構造体を標準ライブラリ「 GUI 」と同様の感覚でそのまま扱えるように、
 * 標準GUIライブラリ関数のオーバーロードも提供されます。
 * <br />
 * 例えば、 
 * <a href="#mountComponent">mountComponent</a> 関数や、
 * <a href="#setComponentSize">setComponentSize</a> 関数、 
 * <a href="#setComponentLocation">setComponentLocation</a> 関数などが提供されます。
 * </p>
 * 
 * @author  松井文宏 - Fumihiro Matsui ( RINEARN )
 * @license Public domain ( CC0 )
 */


/** デフォルトのウィンドウタイトルです。 */
private const string DEFAULT_WINDOW_TITLE = "ButtonWindow";

/** デフォルトのウィンドウオフセット値です。ウィンドウ外周と内部の高さのおおまかな差を指定します。 */
private const int DEFAULT_WINDOW_WIDTH_OFFSET = 16;

/** デフォルトのウィンドウオフセット値です。ウィンドウ外周と内部の高さのおおまかな差を指定します。 */
private const int DEFAULT_WINDOW_HEIGHT_OFFSET = 38;

/** デフォルトのウィンドウ位置(X)です。 */
private const int DEFAULT_WINDOW_X = 0;

/** デフォルトのウィンドウ位置(Y)です。 */
private const int DEFAULT_WINDOW_Y = 0;

/** デフォルトのウィンドウ幅です。 */
private const int DEFAULT_WINDOW_WIDTH = 300;

/** デフォルトのウィンドウ高さです。 */
private const int DEFAULT_GRID_HEIGHT = 30;

/** デフォルトのウィンドウ内余白領域です(左)。 */
private const int DEFAULT_WINDOW_MARGIN_LEFT = 10;

/** デフォルトのウィンドウ内余白領域です(右)。 */
private const int DEFAULT_WINDOW_MARGIN_RIGHT = 10;

/** デフォルトのウィンドウ内余白領域です(上)。 */
private const int DEFAULT_WINDOW_MARGIN_TOP = 10;

/** デフォルトのウィンドウ内余白領域です(下)。 */
private const int DEFAULT_WINDOW_MARGIN_BOTTOM = 10;


/**
 * ボタンウィンドウの構造体です。
 */
public struct ButtonWindow{
	// ウィンドウのGUI部品IDです。
	protected int window = NULL;
	// ボタンパネルです。
	protected ButtonPanel buttonPanel = NULL;
}


/**
 * ボタンウィンドウを生成します。
 *
 * @param x ウィンドウ左上頂点のX座標
 * @param y ウィンドウ左上頂点のY座標
 * @param width ウィンドウの幅
 * @param height ウィンドウの高さ
 * @param title ウィンドウのタイトル
 * @param labelText 全ボタンのラベル文字列を格納する配列
 * @return 生成したボタンウィンドウ
 */
public ButtonWindow newButtonWindow(int x, int y, int width, int height, string title, string labelText[]){
	ButtonWindow windowInstance;

	windowInstance.buttonPanel = newButtonPanel(
		DEFAULT_WINDOW_MARGIN_LEFT,
		DEFAULT_WINDOW_MARGIN_TOP,
		width - DEFAULT_WINDOW_MARGIN_LEFT - DEFAULT_WINDOW_MARGIN_RIGHT - DEFAULT_WINDOW_WIDTH_OFFSET,
		height - DEFAULT_WINDOW_MARGIN_TOP - DEFAULT_WINDOW_MARGIN_BOTTOM - DEFAULT_WINDOW_HEIGHT_OFFSET,
		labelText
	);
	windowInstance.window = newWindow(x, y, width, height, title);
	mountComponent(windowInstance.buttonPanel, windowInstance.window);
	paintComponent(windowInstance.buttonPanel);
	paintComponent(windowInstance.window);
	return windowInstance;
}


/**
 * 自動でウィンドウサイズを決定し、ボタンウィンドウを生成します。
 *
 * @param labelText 全ボタンのラベル文字列を格納する配列
 * @return 生成したボタンウィンドウ
 */
public ButtonWindow newButtonWindow(string labelText[]){
	int n = length(labelText, 0);
	int windowHeight = DEFAULT_GRID_HEIGHT*n + DEFAULT_WINDOW_MARGIN_TOP + DEFAULT_WINDOW_MARGIN_BOTTOM + DEFAULT_WINDOW_HEIGHT_OFFSET;
	ButtonWindow windowInstance = newButtonWindow(
		DEFAULT_WINDOW_X,
		DEFAULT_WINDOW_Y,
		DEFAULT_WINDOW_WIDTH,
		windowHeight,
		DEFAULT_WINDOW_TITLE,
		labelText
	);
	return windowInstance;
}


/**
 * ボタンウィンドウを破棄します。
 *
 * @param windowInstance 破棄するウィンドウ
 */
public void deleteComponent(ButtonWindow &windowInstance){
	demountComponent(windowInstance.buttonPanel, windowInstance.window);
	deleteComponent(windowInstance.buttonPanel);
	deleteComponent(windowInstance.window);
	windowInstance = NULL;
}


/**
 * ウィンドウのGUIコンポーネントIDを取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @return ウィンドウのGUIコンポーネントID
 */
public int getWindow(ButtonWindow &windowInstance){
	return windowInstance.window;
}


/**
 * 指定されたラベルに対応するボタンを取得します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 * @param labelText 取得対象のボタンに対応するラベル文字列
 * @return ボタンのGUIコンポーネントID
 */
public int getButton(ButtonWindow &windowInstance, string labelText){
	return getButton(windowInstance.buttonPanel, labelText);
}


/**
 * 指定されたインデックスのボタンを取得します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 * @param index ボタンのインデックス
 * @return ボタンのGUIコンポーネントID
 */
public int getButton(ButtonWindow &windowInstance, int index){
	return getButton(windowInstance.buttonPanel, index);
}


/*
 * ボタンパネルを取得します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 * @return ボタンパネル構造体
 */
/*
public ButtonPanel getButtonPanel(ButtonWindow &windowInstance){
	return windowInstance.buttonPanel;
}
*/


/**
 * ウィンドウのサイズを設定します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 * @param width 幅
 * @param width 高さ
 * @return ウィンドウのサイズを格納する配列
 */
public void setComponentSize(ButtonWindow &windowInstance, int width, int height){
	setComponentSize(windowInstance.window, width, height);
	setComponentSize(windowInstance);
}


/**
 * ウィンドウの現在のサイズに合わせて、内部コンポーネントのサイズを自動設定します。ウィンドウのサイズを外部から変更した際に使用します。
 *
 * @param windowInstance 対象のウィンドウ
 */
public void setComponentSize(ButtonWindow &windowInstance){
	int windowSize[] = getComponentSize(windowInstance.window);

	int panelWidth = windowSize[0]
		 - DEFAULT_WINDOW_WIDTH_OFFSET - DEFAULT_WINDOW_MARGIN_RIGHT - DEFAULT_WINDOW_MARGIN_LEFT;

	int panelHeight = windowSize[1]
		 - DEFAULT_WINDOW_HEIGHT_OFFSET - DEFAULT_WINDOW_MARGIN_TOP - DEFAULT_WINDOW_MARGIN_BOTTOM;

	setComponentSize(windowInstance.buttonPanel, panelWidth, panelHeight);
}


/**
 * ウィンドウのサイズを返します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 * @return ウィンドウのサイズを格納する配列
 */
public int[] getComponentSize(ButtonWindow &windowInstance){
	return getComponentSize(windowInstance.window);
}


/**
 * ウィンドウの位置を返します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 * @return ウィンドウの位置を格納する配列
 */
public int[] getComponentLocation(ButtonWindow &windowInstance){
	return getComponentLocation(windowInstance.window);
}


/**
 * ウィンドウの位置を設定します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 * @param x ウィンドウのX座標
 * @param y ウィンドウのY座標
 */
public void setComponentLocation(ButtonWindow &windowInstance, int x, int y){
	setComponentLocation(windowInstance.window, x, y);
}


/**
 * ウィンドウを再描画します。
 *
 * @param windowInstance 対象のウィンドウ
 */
public void paintComponent(ButtonWindow &windowInstance){
	paintComponent(windowInstance.buttonPanel);
	paintComponent(windowInstance.window);
}


/**
 * ウィンドウを不可視化します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 */
public void hideComponent(ButtonWindow &windowInstance){
	hideComponent(windowInstance.window);
}


/**
 * ウィンドウを可視化します。
 *
 * @param windowInstance 対象のボタンウィンドウ
 */
public void showComponent(ButtonWindow &windowInstance){
	showComponent(windowInstance.window);
}


