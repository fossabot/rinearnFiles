coding UTF-8;

# PRAGMA COMPILE    REFERRAL
# PRAGMA RESERVE    ALL
# PRAGMA END

import GUI;
import gui.TextFieldPanel;

/**
 * <p>
 * このライブラリ gui.TextFieldWindow は、
 * GUI部品として、テキストフィールドが並ぶウィンドウ ―― テキストフィールドウィンドウ ―― を提供します。	
 * </p>
 *
 * <p>
 * VCSSLの標準ライブラリ「 GUI 」では、GUI部品をint型のIDで扱いますが、
 * このモジュールで提供されるGUI部品は、
 * <a href="#TextFieldWindow">TextFieldWindow</a> 構造体として扱います。
 * </p>
 *
 * <p>
 * TextFieldWindow 構造体は、内部にリソースのID等を保持していますが、
 * <br />
 * 生成・破棄のための関数が提供されるため、その中身を特に意識する必要はありません（ むしろメンバへの直接的なアクセスは推奨されません ）。
 * <br />
 * 具体的には、 <a href="#newTextFieldWindow">newTextFieldWindow</a> 関数で生成し、 
 * <a href="#deleteComponent">deleteComponent</a> 関数で破棄します。
 * </p>
 *
 * <p>
 * またこのモジュールでは、
 * TextFieldWindow 構造体を標準ライブラリ「 GUI 」と同様の感覚でそのまま扱えるように、
 * 標準GUIライブラリ関数のオーバーロードも提供されます。
 * <br />
 * 例えば、 
 * <a href="#mountComponent">mountComponent</a> 関数や、
 * <a href="#setComponentSize">setComponentSize</a> 関数、 
 * <a href="#setComponentLocation">setComponentLocation</a> 関数などが提供されます。
 * </p>
 * 
 * @author  松井文宏 - Fumihiro Matsui ( RINEARN )
 * @license Public domain ( CC0 )
 */


/** デフォルトのウィンドウタイトルです。 */
private const string DEFAULT_WINDOW_TITLE = "TextFieldWindow";

/** デフォルトのウィンドウオフセット値です。ウィンドウ外周と内部の高さのおおまかな差を指定します。 */
private const int DEFAULT_WINDOW_WIDTH_OFFSET = 16;

/** デフォルトのウィンドウオフセット値です。ウィンドウ外周と内部の高さのおおまかな差を指定します。 */
private const int DEFAULT_WINDOW_HEIGHT_OFFSET = 38;

/** デフォルトのウィンドウ位置(X)です。 */
private const int DEFAULT_WINDOW_X = 0;

/** デフォルトのウィンドウ位置(Y)です。 */
private const int DEFAULT_WINDOW_Y = 0;

/** デフォルトのウィンドウ幅です。 */
private const int DEFAULT_WINDOW_WIDTH = 300;

/** デフォルトのウィンドウ高さです。 */
private const int DEFAULT_GRID_HEIGHT = 25;

/** デフォルトのボタン高さです。 */
private const int DEFAULT_BUTTON_HEIGHT = 30;

/** デフォルトのボタン間余白です。 */
private const int DEFAULT_BUTTON_MARGIN_TOP = 5;

/** デフォルトのウィンドウ内余白領域です(左)。 */
private const int DEFAULT_WINDOW_MARGIN_LEFT = 10;

/** デフォルトのウィンドウ内余白領域です(右)。 */
private const int DEFAULT_WINDOW_MARGIN_RIGHT = 10;

/** デフォルトのウィンドウ内余白領域です(上)。 */
private const int DEFAULT_WINDOW_MARGIN_TOP = 10;

/** デフォルトのウィンドウ内余白領域です(下)。 */
private const int DEFAULT_WINDOW_MARGIN_BOTTOM = 10;


/** テキストフィールドウィンドウの構造体です。 */
public struct TextFieldWindow{
	// ウィンドウのGUI部品IDです。
	protected int window = NULL;
	// ボタンのGUI部品IDです。
	protected int button = NULL;
	// テキストフィールドパネルです。
	protected TextFieldPanel textFieldPanel = NULL;
}


/**
 * 自動でウィンドウサイズを決定し、テキストフィールドウィンドウを生成します。
 *
 * @param x ウィンドウ左上頂点のX座標
 * @param y ウィンドウ左上頂点のY座標
 * @param width ウィンドウの幅
 * @param height ウィンドウの高さ
 * @param title ウィンドウのタイトル
 * @param labelText 全テキストフィールドのラベル文字列を格納する配列
 * @param defaultValue 全テキストフィールドのデフォルト値を格納する配列
 * @return 生成したテキストフィールドウィンドウ
 */
public TextFieldWindow newTextFieldWindow(int x, int y, int width, int height, string title, string labelText[], string defaultValue[]){
	TextFieldWindow windowInstance;

	windowInstance.textFieldPanel = newTextFieldPanel(
		DEFAULT_WINDOW_MARGIN_LEFT,
		DEFAULT_WINDOW_MARGIN_TOP,
		width - DEFAULT_WINDOW_MARGIN_LEFT - DEFAULT_WINDOW_MARGIN_RIGHT - DEFAULT_WINDOW_WIDTH_OFFSET,
		height - DEFAULT_WINDOW_MARGIN_TOP - DEFAULT_WINDOW_MARGIN_BOTTOM - DEFAULT_WINDOW_HEIGHT_OFFSET,
		labelText,
		defaultValue
	);

	windowInstance.window = newWindow(x, y, width, height, title);
	mountComponent(windowInstance.textFieldPanel, windowInstance.window);
	paintComponent(windowInstance.textFieldPanel);
	paintComponent(windowInstance.window);

	return windowInstance;
}


/**
 * 自動でウィンドウサイズを決定し、テキストフィールドウィンドウを生成します。
 *
 * @param labelText 全テキストフィールドのラベル文字列を格納する配列
 * @param defaultText 全テキストフィールドのデフォルト値を格納する配列
 * @return 生成したテキストフィールドウィンドウ
 */
public TextFieldWindow newTextFieldWindow(string labelText[], string defaultValue[]){
	int n = length(labelText, 0);
	int windowHeight = DEFAULT_GRID_HEIGHT*n + DEFAULT_WINDOW_MARGIN_TOP + DEFAULT_WINDOW_MARGIN_BOTTOM + DEFAULT_WINDOW_HEIGHT_OFFSET;
	TextFieldWindow windowInstance = newTextFieldWindow(
		DEFAULT_WINDOW_X,
		DEFAULT_WINDOW_Y,
		DEFAULT_WINDOW_WIDTH,
		windowHeight,
		DEFAULT_WINDOW_TITLE,
		labelText,
		defaultValue
	);
	return windowInstance;
}


/**
 * テキストフィールドウィンドウを破棄します。
 *
 * @param windowInstance 破棄するウィンドウ
 */
public void deleteComponent(TextFieldWindow &windowInstance){
	demountComponent(windowInstance.textFieldPanel, windowInstance.window);
	deleteComponent(windowInstance.textFieldPanel);
	deleteComponent(windowInstance.window);
	windowInstance = NULL;
}


/**
 * ラベル部分の幅を設定します。
 *
 * @param windowInstance 取得対象のテキストフィールドウィンドウ
 * @param labelWidth ラベル幅
 */
public void setTextLabelWidth(TextFieldWindow &windowInstance, int labelWidth){
	setTextLabelWidth(windowInstance.textFieldPanel, labelWidth);
}


/**
 * ウィンドウ下部に、決定ボタンを出現させます。
 *
 * @param windowInstance 対象のウィンドウ
 * @param 決定ボタンのラベル文字列
 */
public void setButton(TextFieldWindow &windowInstance, string label){
	if(windowInstance.button == NULL){
		int windowSize[] = getComponentSize(windowInstance.window);
		int panelSize[] = getComponentSize(windowInstance.textFieldPanel);
		int panelLocation[] = getComponentLocation(windowInstance.textFieldPanel);
		int buttonX = panelLocation[0];
		int buttonY = panelLocation[1] + panelSize[1] + DEFAULT_BUTTON_MARGIN_TOP;
		int buttonWidth = panelSize[0];
		int buttonHeight = DEFAULT_BUTTON_HEIGHT;
		windowInstance.button = newButton(
			buttonX, buttonY, buttonWidth, buttonHeight, label
		);
		mountComponent(windowInstance.button, windowInstance.window);
		setComponentSize(windowInstance.window, windowSize[0], windowSize[1] + buttonHeight + DEFAULT_BUTTON_MARGIN_TOP);
	}else{
		setComponentString(windowInstance.button, label);
	}
}


/**
 * 決定ボタンのGUIコンポーネントIDを取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @return 決定ボタンのGUIコンポーネントID
 */
public int getButton(TextFieldWindow &windowInstance){
	return windowInstance.button;
}


/**
 * ウィンドウのGUIコンポーネントIDを取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @return ウィンドウのGUIコンポーネントID
 */
public int getWindow(TextFieldWindow &windowInstance){
	return windowInstance.window;
}


/*
 * 指定されたインデックスのチェックボックスに、string値を設定します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param index チェックボックスのインデックス
 * @param value 設定するstring値
 */
/*
public void setComponentString(TextFieldWindow &windowInstance, int index, string value){
	setComponentString(windowInstance.textFieldPanel, index, value);
}
*/


/*
 * 指定されたインデックスのチェックボックスから、string値を取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param index チェックボックスのインデックス
 * @return テキストフィールドの保持するstring値
 */
/*
public string getComponentString(TextFieldWindow &windowInstance, int index){
	return getComponentString(windowInstance.textFieldPanel, index);
}
*/


/*
 * 指定されたラベルに対応するチェックボックスに、string値を設定します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param labelText 設定対象のテキストフィールドに対応するラベル文字列
 * @param value 設定するstring値
 */
/*
public void setComponentString(TextFieldWindow &windowInstance, string labelText, string value){
	setComponentString(windowInstance.textFieldPanel, labelText, value);
}
*/


/*
 * 指定されたラベルに対応するチェックボックスから、string値を取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param labelText 取得対象のチェックボックスに対応するラベル文字列
 * @return テキストフィールドの保持するstring値
 */
/*
public string getComponentString(TextFieldWindow &windowInstance, string labelText){
	return getComponentString(windowInstance.textFieldPanel, labelText);
}
*/


/**
 * 指定されたラベルに対応するチェックボックスを取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param labelText 取得対象のチェックボックスに対応するラベル文字列
 * @return チェックボックスのGUIコンポーネントID
 */
public int getTextField(TextFieldWindow &windowInstance, string labelText){
	return getTextField(windowInstance.textFieldPanel, labelText);
}


/**
 * 指定されたインデックスのチェックボックスを取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param index チェックボックスのインデックス
 * @return チェックボックスのGUIコンポーネントID
 */
public int getTextField(TextFieldWindow &windowInstance, int index){
	return getTextField(windowInstance.textFieldPanel, index);
}


/*
 * テキストフィールドパネルを取得します。
 *
 * @param windowInstance 対象のウィンドウ
 * @return テキストフィールドパネル構造体
 */
/*
public TextFieldPanel getTextFieldPanel(TextFieldWindow &windowInstance){
	return windowInstance.textFieldPanel;
}
*/


/**
 * ウィンドウのサイズを返します。
 *
 * @param windowInstance 対象のウィンドウ
 * @return ウィンドウのサイズを格納する配列
 */
public int[] getComponentSize(TextFieldWindow &windowInstance){
	return getComponentSize(windowInstance.window);
}


/**
 * ウィンドウのサイズを設定します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param width 幅
 * @param width 高さ
 * @return ウィンドウのサイズを格納する配列
 */
public void setComponentSize(TextFieldWindow &windowInstance, int width, int height){
	setComponentSize(windowInstance.window, width, height);
	setComponentSize(windowInstance);
}


/**
 * ウィンドウの現在のサイズに合わせて、内部コンポーネントのサイズを自動設定します。ウィンドウのサイズを外部から変更した際に使用します。
 *
 * @param windowInstance 対象のウィンドウ
 */
public void setComponentSize(TextFieldWindow &windowInstance){
	int windowSize[] = getComponentSize(windowInstance.window);

	int panelWidth = windowSize[0]
		 - DEFAULT_WINDOW_WIDTH_OFFSET - DEFAULT_WINDOW_MARGIN_RIGHT - DEFAULT_WINDOW_MARGIN_LEFT;

	int panelHeight = windowSize[1]
		 - DEFAULT_WINDOW_HEIGHT_OFFSET - DEFAULT_WINDOW_MARGIN_TOP - DEFAULT_WINDOW_MARGIN_BOTTOM;

	if(windowInstance.button != NULL){
		panelHeight -= DEFAULT_BUTTON_HEIGHT + DEFAULT_BUTTON_MARGIN_TOP;

		int buttonWidth = panelWidth;
		setComponentSize(windowInstance.button, buttonWidth, DEFAULT_BUTTON_HEIGHT);

		int buttonHeight = panelHeight + DEFAULT_WINDOW_MARGIN_TOP + DEFAULT_BUTTON_MARGIN_TOP;
		setComponentLocation(windowInstance.button, DEFAULT_WINDOW_MARGIN_LEFT, buttonHeight);
	}

	setComponentSize(windowInstance.textFieldPanel, panelWidth, panelHeight);
}


/**
 * ウィンドウの位置を返します。
 *
 * @param windowInstance 対象のウィンドウ
 * @return ウィンドウの位置を格納する配列
 */
public int[] getComponentLocation(TextFieldWindow &windowInstance){
	return getComponentLocation(windowInstance.window);
}


/**
 * ウィンドウの位置を返します。
 *
 * @param windowInstance 対象のウィンドウ
 * @return ウィンドウの位置を格納する配列
 */
public int[] getComponentLocation(TextFieldWindow &windowInstance){
	return getComponentLocation(windowInstance.window);
}


/**
 * ウィンドウの位置を設定します。
 *
 * @param windowInstance 対象のウィンドウ
 * @param x ウィンドウのX座標
 * @param y ウィンドウのY座標
 */
public void setComponentLocation(TextFieldWindow &windowInstance, int x, int y){
	setComponentLocation(windowInstance.window, x, y);
}


/**
 * ウィンドウを再描画します。
 *
 * @param windowInstance 対象のウィンドウ
 */
public void paintComponent(TextFieldWindow &windowInstance){
	paintComponent(windowInstance.textFieldPanel);
	if(windowInstance.button != NULL){
		paintComponent(windowInstance.button);
	}
	paintComponent(windowInstance.window);
}


/**
 * ウィンドウを不可視化します。
 *
 * @param windowInstance 対象のウィンドウ
 */
public void hideComponent(TextFieldWindow &windowInstance){
	hideComponent(windowInstance.window);
}


/**
 * ウィンドウを可視化します。
 *
 * @param windowInstance 対象のウィンドウ
 */
public void showComponent(TextFieldWindow &windowInstance){
	showComponent(windowInstance.window);
}


