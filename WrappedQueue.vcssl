coding UTF-8;

# PRAGMA COMPILE    REFERRAL
# PRAGMA RESERVE    ALL
# PRAGMA END

/**
 * <p>
 * このライブラリ data.WrappedQueue は、
 * キュー型のデータ構造を提供します。	
 * </p>
 *
 * <p>
 * このモジュールでは、スタックのデータを保持する
 * ジェネリック構造体 <a href="#WrappedQueue">WrappedQueue</a> と、
 * それに対してデータを出し入れするジェネリック関数 
 * <a href="#enqueue">enqueue</a>, <a href="#dequeue">dequeue</a> 
 * などが提供されます。 
 * </p>
 *
 * <p>
 * なお、この data.WrappedQueue では、
 * 要素を <a href="DataWrapper.html">DataWrapper</a> 構造体で
 * ラッピングして保持する実装になっています。
 * <br />
 * そのため WrappedQueueでは、 
 * data.<a href="Queue.html">Queue</a> とは異なり、
 * 配列を要素として扱う事ができます。
 * </p>
 *
 * <p>
 * 反面、要素の出し入れの際に DataWrapper 構造体への
 * ラッピング処理を伴うため、そのオーバーヘッドの分だけ、
 * Queue よりも処理が重くなります。
 * <br />
 * 従って、要素が配列でない場合には、
 * WrappedQueue よりも Queue の使用が推奨されます。
 * </p>
 * 
 * @author  松井文宏 - Fumihiro Matsui ( RINEARN )
 * @license Public domain ( CC0 )
 */


import data.DataWrapper;


/**
 * キュー構造体です。キューのデータを保持します。
 */
public struct WrappedQueue<Type>{
	protected int first = 0;
	protected int last = 0;
	protected int size = 0;
	protected DataWrapper<Type> data[1];
}


/**
 * 要素を全て削除します。
 *
 * @param queue 対象のキュー構造体
 */
public void clear<Type>( WrappedQueue<Type> &queue ){
	alloc[0] queue.data;
	alloc[1] queue.data;
	queue.first = 0;
	queue.last = 0;
	queue.size = 0;
}


/**
 * 使用中のサイズを返します。
 *
 * @param queue 対象のキュー構造体
 */
public int size<Type>( WrappedQueue<Type> &queue ){
	return queue.size;
}


/**
 * キューに要素を追加します。
 *
 * @param queue 対象のキュー構造体
 * @param value 追加する要素
 */
public void enqueue<Type>( WrappedQueue<Type> &queue, Type value ){
	queue.size++;
	queue.data[ queue.last++ ] = wrap<Type>(value);
	setProperLength<Type>(queue);
	queue.last %= length(queue.data, 0);
}


/**
 * キューから要素を取り出します。取り出した要素は、キューから削除されます。
 *
 * @param queue 対象のキュー構造体
 * @return 取り出した要素
 */
public Type dequeue<Type>( WrappedQueue<Type> &queue ){
	queue.size--;
	checksize(queue.size);
	Type value = unwrap<Type>(queue.data[ queue.first++ ]);
	queue.first %= length(queue.data, 0);
	setProperLength<Type>(queue);
	return value;
}


/**
 * キューから要素を削除せずに取り出します。
 *
 * @param queue 対象のキュー構造体
 * @return 取り出した要素
 */
public Type peek<Type>( WrappedQueue<Type> &queue ){
	return unwrap<Type>(queue.data[ queue.first ]);
}


/**
 * キューが指定された要素を含んでいるか判定します。
 *
 * @param queue 対象のキュー構造体
 * @param 含まれるか判定したい要素
 */
public bool contains<Type>( WrappedQueue<Type> &queue, Type element ){
	int n = queue.size;
	DataWrapper<Type> wrappedElement = wrap<Type>(element);
	for(int i=0; i<n; i++){
		if(queue.data[i] == wrappedElement){
			return true;
		}
	}
	return false;
}


/**
 * キューが保持する全要素の順序を反転させます。
 *
 * @param queue 対象のキュー構造体
 */
public void reverse<Type>( WrappedQueue<Type> &queue ){

	// queue.dataはリングバッファなので、内容をシフトして先頭をインデックス 0 に移動
	shiftData<Type>( queue );

	int n = queue.size;
	DataWrapper<Type> rev[n];
	for(int i=0; i<n; i++){
		rev[i] = queue.data[n-1-i];
	}
	queue.data = rev;
}


/**
 * キューが保持する全要素を、DataWrapper配列に変換して返します。
 *
 * @param queue 対象のキュー構造体
 * @return スタックの全要素をまとめたDataWrapper配列
 */
public DataWrapper<Type>[] toDataWrapperArray<Type>( WrappedQueue<Type> &queue ){

	// queue.dataはリングバッファなので、内容をシフトして先頭をインデックス 0 に移動
	shiftData<Type>( queue );

	// バッファの余剰領域を削除して返す
	DataWrapper<Type> array[] = queue.data;
	alloc[ queue.size ] array;
	return array;
}


/**
 * キューバッファを、必要に応じて最適な容量に再確保します。
 *
 * @param queue 対象のキュー構造体
 */
private int setProperLength<Type>( WrappedQueue<Type> &queue ){

	//<varint>や<varfloat>の場合、式中でintやfloatのリテラルを使うと精度警告が出るので、変数化して回避
	int zero = 0;

	//現在のバッファ長と、適切なバッファ長を取得
	int currentLength = length(queue.data, zero);
	int currentUsage = getUsage(queue.first, queue.last, currentLength);
	int properLength = getProperLength(currentLength, currentUsage);

	//現在のバッファサイズが適切でない場合は容量変更
	if(currentLength != properLength){

		//リングバッファをシフトして先頭をインデックス 0 に移動
		shiftData<Type>( queue );

		//バッファ容量変更
		alloc[properLength] queue.data;
	}

}


/**
 * キューのサイズが適正かどうかを確認し、不正ならエラーを出力します。
 *
 * @param size キューのサイズ
 */
private int checksize(int size){
	if(size < 0){
		error("キューの容量が負になりました。");
	}
}


/**
 * キューバッファ（リングバッファ）をシフトし、
 * 先頭をインデックス 0 に合わせます。
 *
 * @param queue 対象のキュー構造体
 */
private void shiftData<Type>( WrappedQueue<Type> &queue ){
	int zero = 0;
	int currentLength = length(queue.data, zero);

	DataWrapper<Type> shiftedData[] = queue.data;
	int f = queue.first;
	for(int i=0; i<currentLength; i++){
		shiftedData[i] = queue.data[ (i+f) % currentLength ];
	}
	queue.data = shiftedData;

	// 位置情報を変更して再登録
	if(queue.last < queue.first){
		queue.last += currentLength;
	}
	queue.last -= queue.first;
	queue.first = 0;
}


/**
 * 現在のキューバッファ長と、その中の使用量から、最適なキューバッファ長を返します。
 *
 * @param length 現在のキューバッファ長
 * @param usage 現在のキューバッファ使用量
 */
private int getProperLength(int length, int usage){
	if( length == 0 ){
		return 1;
	}else if( length <= usage+1 ){
		return length * 2;
	}else if( usage < length/2 ){
		return length / 2;
	}else{
		return length;
	}
}


/**
 * キューのリングバッファ内で、実際に使用している長さを返します。
 *
 * @param first 先頭インデックス
 * @param last 終端インデックス
 * @param length バッファ長
 */
private int getUsage(int first, int last, int length){
	if(first <= last){
		return last - first;
	}else{
		return last - first + length;
	}
}





